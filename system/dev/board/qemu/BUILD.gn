# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

driver("qemu") {
  sources = [
    "qemu-bus.c",
    "qemu-test.c",
  ]
  deps = [
    "$zx/system/banjo/ddk-protocol-platform-bus",
    "$zx/system/banjo/ddk-protocol-platform-device",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/zircon",
  ]
  if (current_cpu == "arm64") {
    data_deps = [
      "$zx/kernel/target/arm64/boot-shim:qemu",
    ]
  }
}

test_drivers = [
  "child-1",
  "child-2",
  "child-3",
  "parent",
]

group("qemu-test") {
  deps = []
  foreach(test, test_drivers) {
    deps += [ ":qemu-$test" ]
  }
}

foreach(test, test_drivers) {
  driver("qemu-$test") {
    sources = [
      "test/$test.c",
    ]
    deps = [
      "$zx/system/banjo/ddk-protocol-platform-device",
      "$zx/system/ulib/ddk",
      "$zx/system/ulib/zircon",
    ]
  }
}
