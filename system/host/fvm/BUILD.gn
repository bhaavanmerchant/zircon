# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This is shared with $zx/system/utest/fvm-host.
source_set("common") {
  sources = [
    "container/container.cpp",
    "container/fvm.cpp",
    "container/sparse.cpp",
    "format/blobfs.cpp",
    "format/format.cpp",
    "format/minfs.cpp",
  ]
  public_deps = [
    ":config",
    "$zx/system/ulib/blobfs",
    "$zx/system/ulib/digest",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/fit",
    "$zx/system/ulib/fs",
    "$zx/system/ulib/fvm",
    "$zx/system/ulib/minfs",
    "$zx/third_party/ulib/lz4",
    "$zx/third_party/ulib/uboringssl",

    # include/fvm/format.h has #include <fs-management/mount.h>
    # though that library is not really meant for host code.
    "$zx/system/ulib/fs-management:headers",
  ]
}

config_group("config") {
  visibility = [ ":*" ]
  include_dirs = [ "include" ]
}

host_tool("fvm") {
  sources = [
    "main.cpp",
  ]
  include_dirs = [ "include" ]
  deps += [ ":common" ]
}
