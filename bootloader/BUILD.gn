# Copyright 2018 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("$zx/public/gn/toolchain/environment.gni")
import("$zx/public/gn/toolchain/select_toolchain.gni")

# Gigaboot gets its own toolchains to build EFI code.
if (current_toolchain == default_toolchain) {
  define_environment("efi") {
    cpu = "x64"
    os = "win"
    globals = {
      is_kernel = true
    }
    configs += standard_fuchsia_configs + [ ":efi_config" ]

    # TODO: harmless, but just to match build.mk
    configs -= [ "$zx/public/gn/config:default_frame_pointers" ]

    # TODO: strip = true
  }
} else if (toolchain.environment == "efi") {
  # This is the top config for all code in the efi_toolchain.
  config("efi_config") {
    public_deps = [
      "$zx/kernel:standalone",
      "$zx/public/gn/config:no_sanitizers",

      # TODO: "$zx/kernel:warnings",
    ]
    include_dirs = [ "include" ]
    cflags = [
      "-std=c99",
      "-fshort-wchar",
    ]
    if (current_cpu == "x64") {
      cflags += [ "-mno-red-zone" ]
    }
  }

  executable("bootloader") {
    output_dir = root_out_dir
    output_name = "boot${current_cpu}"
    output_extension = "efi"
    deps = [
      "lib",
      "src",
    ]
    ldflags = [
      "-nostdlib",
      "-Wl,/subsystem:efi_application",
      "-Wl,/entry:efi_main",
    ]
  }
} else {
  # In any other toolchain, just redirect to the proper toolchain.
  select_toolchain("bootloader") {
    environment_label = ":efi"
    deps = [
      ":bootloader",
    ]
  }
}
